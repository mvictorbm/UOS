///////////////////////////////////////////////////////////////////////////
///////////////////////////// SETUP ///////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
//Timers
if not timerexists 'evasion'
  @settimer 'evasion' 22000
endif
if not timerexists 'nocast'
  @settimer 'nocast' 2000
endif
if not timerexists 'lure'
  @settimer 'lure' 10000
endif
//Logout if dead
if dead 'self'
  if @inrange 'mount' 5
    logoutbutton
  else
    stop
  endif
endif
//Replicas
if not listexists 'replicas'
  createlist 'replicas'
  @pushlist! 'replicas' 0x1413
  @pushlist! 'replicas' 0x1540
  @pushlist! 'replicas' 0x1541
  @pushlist! 'replicas' 0x1f03
  @pushlist! 'replicas' 0x2684
  @pushlist! 'replicas' 0x170b
  @pushlist! 'replicas' 0x1f0b
endif
@setalias 'mount' 0x3ef74
sysmsg "# BOT Champ by Grifo (Llama's Mod) #" 15
///////////////////////////////////////////////////////////////////////////
////////////////////// END OF SETUP ///////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
//Main loop
while not dead 'self'
  @setability 'primary' 'on'
  if not buffexists "Dismount" and @inrange 'mount' 2
    if not mounted 'self' and not poisoned 'mount'
      useobject! 'mount'
    endif
  endif
  //Open doors
  if @findtype 0x679 'any' 'ground' 1 2 or @findtype 0x6c9 'any' 'ground' 1 2
    useobject! 'found'
    pause 650
  endif
  //Valor if inactive
  if @findtype 0x1f18 0 'ground' 1 10 and timer 'nocast' > 2000
    @setalias 'idol' 'found'
    if @findtype 0x75a 1175 'ground'
      virtue 'valor'
      waitfortarget 500
      target! 'idol'
      settimer 'nocast' 0
    endif
  endif
  //Send money
  if @findtype 0xe76 'any' 'backpack' 1 0
    waitforproperties 'found' 1000
    if @property 'Charges' 'found' <= 5
      //headmsg '# Atenção - Bag of Sending #' 63
    elseif @property 'Charges' 'found' == 0
      headmsg '# Bag of Sending charges: 0 #' 63
    endif
    if weight 'self' > 430
      if usetype! 0xe76
        waitfortarget 1000
        targettype! 0xeed
        pause 500
      endif
    endif
  endif
  //Check replicas
  for 0 to 'replicas'
    canceltarget
    while @findtype 'replicas[]' 'any' 'backpack'
      if @color 'found' != 0
        waitforproperties 'found' 1500
        if @property 'replica' 'found'
          if not @property 'insured' 'found' and not @property 'blessed' 'found'
            waitforcontext 'self' 3 1500
            waitfortarget 1500
            if @target! 'found'
              waitforproperties 'found' 1500
            endif
          endif
        endif
      endif
      ignoreobject! 'found'
    endwhile
  endfor
  //Lure mobs
  if not @inrange 'enemy' 1 and @timer 'lure' > 5000
    for 20
      getenemy 'criminal' 'enemy' 'murderer' 'gray' 'next'
      attack 'enemy'
    endfor
    settimer 'lure' 0
  endif
  //Get enemy
  @getenemy 'criminal' 'enemy' 'murderer' 'gray' 'closest'
  @setability 'primary' 'on'
  if @graphic 'enemy' == 0x33 or @graphic 'enemy' == 0xa4
    @unsetalias 'enemy'
  else
    attack 'enemy'
  endif
  //Cast spells
  if timer 'nocast' > 2000 and @inrange 'enemy' 10
    if not buffexists 'Consecrate Weapon' and mana 'self' >= 15
      @cast 'consecrate weapon'
      @settimer 'nocast' 0
    elseif diffhits > 30
      if not buffexists 'curse weapon' and counttype 0xf8a 0 'backpack' > 1
        cast 'curse Weapon'
        @settimer 'nocast' 1500
      endif
    elseif not @buffexists 'confidence' and hits < 100
      cast 'confidence'
      @settimer 'nocast' 0
    elseif not buffexists 'Counter Attack' and mana >= 10
      cast 'Counter Attack'
      @settimer 'nocast' 1500
    elseif not @buffexists 'evasion' and @timer 'evasion' > 23000
      cast 'evasion'
      @settimer 'nocast' 0
    endif
    if hits > 80 and stam < 150
      if mana 'self' >= 12
        @cast "Divine Fury"
        @settimer 'nocast' 0
      endif
    endif
  endif
  @getenemy 'criminal' 'enemy' 'murderer' 'gray' 'closest'
  if not @inrange 'enemy' 7
    if diffhits > 20 or @poisoned 'self'
      chivalryheal 'self'
      while waitingfortarget
      endwhile
    endif
  endif
endwhile
