//Using findtype instead of getenemy in order to be able to ignore targets after searching them for scrolls
//Scape if successfuly steal, hurt or not hidden
sysmsg 'Star room thief by Llama' 16
@unsetalias 'targetplayer'
@unsetalias 'targetbag'
@clearlist 'tosteal'
@clearignorelist
@ignoreobject 'self'
if not timerexists 't_clearignore'
  createtimer 't_clearignore'
endif
if @findlayer 'self' 1  or @findlayer 'self' 2
  clearhands 'both'
  pause 1200
endif
if not hidden 'self'
  useskill 'hiding'
  pause 100
endif
if not hits == maxhits or poisoned 'self'
  bigheal 'self'
  while waitingfortarget
  endwhile
endif
if not @listexists 'mybodies'
  createlist 'mybodies'
  pushlist 'mybodies' "0x190"
  pushlist 'mybodies' "0x191"
  pushlist 'mybodies' "0x25d"
  pushlist 'mybodies' "0x25e"
  pushlist 'mybodies' "0x29a"
  pushlist 'mybodies' "0x2ec"
  pushlist 'mybodies' "0x2eb"
  pushlist 'mybodies' "0x2ed"
  pushlist 'mybodies' "0x2e8"
  pushlist 'mybodies' "0x2e9"
  pushlist 'mybodies' "0xdc"
  pushlist 'mybodies' "0x19"
endif
if not @listexists 'priority'
  createlist 'priority'
  pushlist 'priority' 'archery'
  pushlist 'priority' 'magery'
  pushlist 'priority' 'evaluating intelligence'
  pushlist 'priority' 'swordsmanship'
  pushlist 'priority' 'tactics'
endif
if not @listexists 'tosteal'
  createlist 'tosteal'
endif
//Main loop
while diffhits < 20 and hidden 'self'
  if timer 't_clearignore' > 600000
    settimer 't_clearignore' 0
    replay
  endif
  if not @inrange 'targetplayer' 2
    for 0 to mybodies
      if @findtype mybodies[] 'any' 'ground' 1 2
        @setalias 'targetplayer' 'found'
        headmsg 'I am the target' 16 'targetplayer'
        playsound 'kick.wav'
        break
      endif
    endfor
  else
    if not @findobject 'targetbag'
      if @findlayer 'targetplayer' 21
        @setalias 'targetbag' 'found'
        headmsg 'Target bag is set' 16
      else
        ignoreobject 'targetplayer'
        @unsetalias 'targetplayer'
        continue
      endif
    endif
    if @inrange 'targetplayer' 2
      useobject! 'targetbag'
      //A runebook or atlas book was found, but no PS. It means you managed to open the bag.
      if @findtype 0x9c16 'any' 'targetbag' or @findtype 0x22c5 'any' 'targetbag'
        if not @findtype 0x14f0 1153 'targetbag'
          sysmsg 'The player has no PS in his bag' 16
          @ignoreobject 'targetplayer'
          @ignoreobject 'targetbag'
          @unsetalias 'targetplayer'
          @unsetalias 'targetbag'
        endif
      endif
      //Check first for priority PS's
      if @findalias 'targetbag'
        while @findtype 0x14f0 1153 'targetbag' and @inrange 'targetplayer' 2
          waitforproperties 'found' 500
          if property 120 'found'
            pushlist 'tosteal' 'found'
            sysmsg 'found a 120 PS' 16
            for 0 to priority
              if property priority[] 'found'
                sysmsg '*** Stealing a priority 120 ***' 16
                useskill 'stealing'
                waitfortarget 500
                target! 'found'
                clearlist 'tosteal'
                break
              endif
            endfor
            @ignoreobject 'found'
          else
            @ignoreobject 'found'
          endif
        endwhile
      endif
      //No priority PS was found stealing a random 120
      if list 'tosteal' > 0 and @inrange 'targetplayer' 2
        sysmsg 'Stealing a random 120' 16
        useskill 'stealing'
        waitfortarget 500
        target! tosteal[0]
        clearlist 'tosteal'
        break
      endif
    endif
  endif
  pause 50
endwhile
if @injournal "You successfully" 'system' or hits < maxhits
  clearjournal
  usetype 0xf6c 0 'ground'
  waitforgump 0xe0e675b8 1500
  replygump 0xe0e675b8 1 300
  pause 1000
endif
if not hidden 'self'
  usetype 0xf6c 0 'ground'
  waitforgump 0xe0e675b8 1500
  replygump 0xe0e675b8 1 300
  pause 1000
endif
if @inregion 'guards' 'self' or dead 'self'
  playsound 'lost.wav'
  stop
endif
