//IDOC looter v.1
//Author: Llama Vortex
//Insert your beetle serial here
@clearlist 'chests'
@unsetalias 'housesign'
setalias 'beetle' 0x1229fd
if not listexists 'chests'
  createlist 'chests'
endif
if not timerexists 'seek'
  createtimer 'seek'
endif
if not timerexists 'btwact'
  createtimer 'btwact'
endif
while not @findalias 'housesign'
  sysmsg 'Select the house sign to be watched' 45
  promptalias 'housesign'
  pause 2000
endwhile
setalias 'selectingchests' 'self'
while hits > 0
  sysmsg 'Select the items you wish to collect (press ESC to stop)' 45
  promptalias 'chest'
  while @targetexists 'system'
  endwhile
  pause 500
  if @findobject 'chest'
    pushlist 'chests' 'chest'
  else
    break
  endif
  @unsetalias 'chest'
endwhile
headmsg 'You are watching this house! Good luck!' 220 'housesign'
pause 1000
headmsg 'The selected items are:' 220
for 0 to chests
  headmsg '** This **' 45 chests[]
  pause 100
endfor
while hits > 0
  if not @findobject 'housesign'
    if timer 'seek' > 5000
      sysmsg 'The house has fallen!!' 45
      sysmsg 'The house has fallen!!' 45
      sysmsg 'The house has fallen!!' 45
      sysmsg 'The house has fallen!!' 45
      sysmsg 'The house has fallen!!' 45
      playsound 'aviso.wav'
    endif
    if list 'chests' == 0
      stop
    endif
    for 0 to chests
      settimer 'seek' 0
      while @inrange chests[] 20
        @headmsg '*get this*' 45 chests[]
        //while timer 'seek' < 10000 and not @inrange chests[] 20
        if not @inrange chests[] 2
          if @x chests[] > x 'self' and @y chests[] > y 'self'
            run 'southeast'
          elseif @x chests[] < x 'self' and @y chests[] > y 'self'
            run 'southwest'
          elseif @x chests[] > x 'self' and @y chests[] < y 'self'
            run 'northeast'
          elseif @x chests[] < x 'self' and @y chests[] < y 'self'
            run 'northwest'
          elseif @x chests[] > x 'self' and @y chests[] == y 'self'
            run 'east'
          elseif @x chests[] < x 'self' and @y chests[] == y 'self'
            run 'west'
          elseif @x chests[] == x 'self' and @y chests[] > y 'self'
            run 'south'
          elseif @x chests[] == x 'self' and @y chests[] < y 'self'
            run 'north'
          endif
        endif
        if @inrange chests[] 2 and timer 'btwact' > 600
          pause 50
          moveitem chests[] 'beetle'
          settimer 'btwact' 0
          break
        endif
        if @timer 'seek' > 10000
          break
        endif
      endwhile
    endfor
  endif
endwhile
