//starts fresh
//Scape if successfuly stolen or hurt
pause 500
if injournal "You successfully" 'system' or hits < 90
  clearjournal
  usetype 0xf6c 0 'ground'
  waitforgump 0xe0e675b8 1500
  replygump 0xe0e675b8 1 300
  pause 1000
  stop
endif
unsetalias 'targetplayer'
unsetalias 'targetbag'
clearlist 'tosteal'
clearignorelist
if not timerexists 't_clearignore'
  createtimer 't_clearignore'
endif
if @findlayer 'self' 1  or @findlayer 'self' 2
  clearhands 'both'
  pause 1200
endif
if not listexists 'mybodies'
  createlist 'mybodies'
  pushlist 'mybodies' "0x190"
  pushlist 'mybodies' "0x191"
  pushlist 'mybodies' "0x25d"
  pushlist 'mybodies' "0x25e"
  pushlist 'mybodies' "0x29a"
  pushlist 'mybodies' "0x2ec"
  pushlist 'mybodies' "0x2eb"
  pushlist 'mybodies' "0x2ed"
  pushlist 'mybodies' "0x2e8"
  pushlist 'mybodies' "0x2e9"
  pushlist 'mybodies' "0xdc"
  pushlist 'mybodies' "0x19"
endif
if not listexists 'priority'
  createlist 'priority'
  pushlist 'priority' 'archery'
  pushlist 'priority' 'magery'
  pushlist 'priority' 'focus'
  pushlist 'priority' 'evaluating intelligence'
  pushlist 'priority' 'swordsmanship'
  pushlist 'priority' 'tactics'
endif
if not @listexists 'tosteal'
  createlist 'tosteal'
endif
while hits > 0
  if not hidden 'self'
    useskill 'hiding'
  endif
  if timer 't_clearignore' > 600000
    settimer 't_clearignore' 0
    replay
  endif
  ignoreobject 'self'
  while not @findobject 'targetplayer'
    for 0 to mybodies
      if @findtype mybodies[] 'any' 'ground' 1 5
        @setalias 'targetplayer' 'found'
        headmsg 'I am the target' 45 'targetplayer'
        break
      endif
    endfor
  endwhile
  if @findlayer 'targetplayer' 21
    @setalias 'targetbag' 'found'
  else
    ignoreobject 'targetplayer'
  endif
  if @inrange 'targetplayer' 2
    useobject! 'targetbag'
  endif
  //A runebook or atlas book was found, but no PS. It means you managed to open the bag.
  //if not @findtype 0x14f0 1153 'targetbag'
  //  if @findtype 0x9c16 'any' 'targetbag' or @findtype 0x22c5 'any' 'targetbag'
  //    sysmsg 'The player has no PS in his bag' 45
  //   ignoreobject 'targetplayer'
  //    ignoreobject 'targetbag'
  //    unsetalias 'targetplayer'
  //    unsetalias 'targetbag'
  //  endif
  //endif
  while @findtype 0x14f0 1153 'targetbag' and @inrange 'targetplayer' 2
    waitforproperties 'found' 500
    if property 120 'found'
      pushlist 'tosteal' 'found'
      //sysmsg 'found a 120 PS' 45
      for 0 to priority
        if property priority[] 'found'
          sysmsg '*** stealing a priority 120 ***' 45
          useskill 'stealing'
          waitfortarget 500
          target! 'found'
          clearlist 'tosteal'
          replay
        endif
      endfor
      ignoreobject 'found'
    else
      ignoreobject 'found'
    endif
  endwhile
  //No priority PS was found stealing a random 120
  if list 'tosteal' > 0 and @inrange 'targetplayer' 2
    sysmsg 'stealing a random 120' 45
    useskill 'stealing'
    waitfortarget 500
    target! tosteal[0]
    clearlist 'tosteal'
    replay
  endif
endwhile
